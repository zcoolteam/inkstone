apply plugin: 'com.android.application'
apply plugin: 'com.zcool.inkstone'

android {
    compileSdkVersion rootProject.ext.bcfg.ver.target_sdk
    buildToolsVersion rootProject.ext.bcfg.ver.build_tools

    // 第三方登录分享参数
    def qqAppId = "101508464"
    def weixinAppKey = "wx35fc52d0224a96f2"
    def weixinAppSecret = "4d413f46bb8aa00e35d6a6116e0e0522"
    def weiboAppKey = "832675406"
    def weiboRedirectUrl = "https://api.weibo.com/oauth2/default.html"

    defaultConfig {
        applicationId "com.zcool.sample"
        minSdkVersion rootProject.ext.bcfg.ver.min_sdk
        targetSdkVersion rootProject.ext.bcfg.ver.target_sdk
        versionCode rootProject.ext.bcfg.ver.versionCode
        versionName rootProject.ext.bcfg.ver.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86'
        }

        multiDexEnabled true
        multiDexKeepFile file("multidex-config.txt")

        flavorDimensions("channel")

        buildConfigField("String", "QQ_APP_ID", "\"${qqAppId}\"")
        buildConfigField("String", "WX_APP_KEY", "\"${weixinAppKey}\"")
        buildConfigField("String", "WX_APP_SECRET", "\"${weixinAppSecret}\"")
        buildConfigField("String", "WEIBO_APP_KEY", "\"${weiboAppKey}\"")
        buildConfigField("String", "WEIBO_REDIRECT_URL", "\"${weiboRedirectUrl}\"")
        manifestPlaceholders = ["inkstone_qq_app_id": "${qqAppId}"]

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['INKSTONE_MODULE_MANIFEST_PACKAGE': "com.zcool.sample"]
            }
        }
    }

    productFlavors {
        zcool {
            dimension "channel"
            buildConfigField("String", "CHANNEL", "\"zcool\"")
        }
    }

    signingConfigs {
        sample {
            storeFile file('../sample_keystore.jks')
            storePassword 'sample'
            keyAlias 'sample'
            keyPassword 'sample'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${defaultConfig.applicationId}_${defaultConfig.versionName}(${defaultConfig.versionCode})_${variant.productFlavors[0].name}_${variant.buildType.name}.apk"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.sample
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            resValue("bool", "inkstone_debug", "false")
            resValue("bool", "inkstone_debug_widget", "false")
        }
        debug {
            signingConfig signingConfigs.sample
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            resValue("bool", "inkstone_debug", "true")
            resValue("bool", "inkstone_debug_widget", "true")
            debuggable true
        }
        dev {
            signingConfig signingConfigs.sample
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled false
            resValue("bool", "inkstone_debug", "true")
            resValue("bool", "inkstone_debug_http_body", "true")
            resValue("bool", "inkstone_debug_widget", "true")
            debuggable true
            matchingFallbacks = "debug"
        }
        devminify {
            signingConfig signingConfigs.sample
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            minifyEnabled true
            resValue("bool", "inkstone_debug", "true")
            resValue("bool", "inkstone_debug_http_body", "true")
            resValue("bool", "inkstone_debug_widget", "true")
            debuggable true
            matchingFallbacks = "debug"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':appmodule')
    implementation project(':appmodule2')
    // api rootProject.ext.bcfg.deps.inkstone
    // annotationProcessor rootProject.ext.bcfg.deps.inkstone_processor
    api project(":inkstone")
    annotationProcessor project(":inkstone-processor")

    implementation rootProject.ext.bcfg.deps.butterknife
    annotationProcessor rootProject.ext.bcfg.deps.butterknife_processor

    // test
    testImplementation rootProject.ext.bcfg.deps.junit
    // android test
    androidTestImplementation rootProject.ext.bcfg.deps.junit
    androidTestImplementation rootProject.ext.bcfg.deps.test_runner
    androidTestImplementation rootProject.ext.bcfg.deps.test_espresso
    // other
    releaseImplementation rootProject.ext.bcfg.deps.leakcanary_no_op
    debugImplementation rootProject.ext.bcfg.deps.leakcanary
    devImplementation rootProject.ext.bcfg.deps.leakcanary
    devminifyImplementation rootProject.ext.bcfg.deps.leakcanary
    testImplementation rootProject.ext.bcfg.deps.leakcanary
    androidTestImplementation rootProject.ext.bcfg.deps.leakcanary
}
